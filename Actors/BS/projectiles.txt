
//Puffs and Blood
Actor BlakePuff : BulletPuff Replaces BulletPuff
{
	+PUFFONACTORS
	+PUFFGETSOWNER
	States
	{
	Spawn:
	Crash:
		TNT1 A 0 
		Stop
	Melee:
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("A_SetAttacker", 0)
		Stop
	}
}

//Used by pistols and PDU - can't hit turrets
Actor BlakePuffNoTurret : BlakePuff
{
	DamageType "NoTurret"
}

Actor BlakeBlood Replaces Blood
{/*Temporary*/}

//Player Projectiles
//Accurate to the original, but looks like crap in-game
ACTOR BSFlame
{
	Radius 10
	Height 8
	Speed 14
	Damage (Random(40, 167))
	Projectile
	SeeSound "plasmagrenade/fire"
	DeathSound "plasmagrenade/hit"
	Obituary ""
	DamageType "NoTurret"
	States
	{
	Spawn:
		PPDU C 0 A_ScaleVelocity(FRandom(1.0, 1.5))
		PPDU CBBABBCD 3 BRIGHT
		PPDU E 0 A_Scream
		PPDU E 0 A_Explode(Random(50, 177), 128, 0)
		PPDU E 0 A_Stop
		PPDU E Random(3, 6) BRIGHT
		PPDU FGH 3 Bright
		Stop
	Death:
		PPDU J 0 A_Explode(Random(50, 177), 128, 0)
		PPDU J Random(2, 5) BRIGHT
		PPDU KLMN 2 BRIGHT
		Stop
	}
}

//Less accurate to original, but looks better and has same effects
ACTOR BSFlameAlt : BSFlame
{
	+BOUNCEONFLOORS
	-NOGRAVITY
	+USEBOUNCESTATE
	Gravity 0.25
	Speed 16
	States
	{
	Spawn:
		PPDU C 5 BRIGHT
		Loop
	Bounce.Floor:
		PPDU E 0 A_Scream
		PPDU E 0 A_Explode(Random(50, 177), 128, 0)
		PPDU E 0 A_Stop
		FEXP A Random(3, 6) BRIGHT
		FEXP BCD 3 Bright
		Stop
	Bounce:
		PPDU J 0 A_Explode(Random(50, 177), 128, 0)
		PPDU J Random(2, 5) BRIGHT
		PPDU KLMN 2 BRIGHT
		Stop
	}
}

//Enemy Projectiles
Actor ProjectileTemplate
{
	PROJECTILE
	Radius 4
	Speed 14
	Height 16
	+CANPASS
//	DeathSound "misc/blake/explosion1"
	DamageType "BS"
	States
	{
		Spawn:
			UNKN A 0
			"####" # 0 A_Jump(256, "Fly")
		Fly:
			"####" ABC 5
			Loop
		Death:
			"####" DEF 4 A_Explode(16, 32, 1)
			Stop
	}
}

Actor SmallCanisterShot : ProjectileTemplate
{
	SeeSound "smallcanisteralien/attack"
	States
	{
		Spawn:
			PBRN A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor Proj_Plasma : ProjectileTemplate
{
	States
	{
	Spawn:
		PPLA AB 5
		Loop
	Death:
		PPLA CD 5 A_Explode(16, 32, 1)
		Stop
	}
}

Actor Proj_Water : ProjectileTemplate
{
	States
	{
		Spawn:
			PPUD A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor Proj_Mech : ProjectileTemplate
{
	States
	{
		Spawn:
			PBLU A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor Proj_BrownBossAlien : ProjectileTemplate
{
	States
	{
		Spawn:
			PORG A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor Proj_X : ProjectileTemplate
{
	SeeSound "spidermutant/attack"
	States
	{
		Spawn:
			PSPI A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor Proj_Green : ProjectileTemplate
{
	States
	{
		Spawn:
			PACD A 0
			"####" # 0 A_Jump(256, "Fly")
	}
}

Actor MapView
{
	Radius 12
	Speed 200
	+MISSILE
	+NOGRAVITY
	+DROPOFF
	+NOBLOCKMAP
	-SOLID
	+THRUACTORS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+NOTONAUTOMAP
	RenderStyle "None"
	BounceFactor 0.0
	BounceCount 5
	States
	{
		Spawn:
			TNT1 A 0
		MappingLoop:
			"####" A 1 A_CheckSight("Dead")
			"####" A 1 A_JumpIfInTargetLOS("Continue", 80.0, JLOSF_CLOSENOFOV, 0, 256.0)
			Goto Dead
		Continue:
			"####" A 0 ACS_NamedExecuteAlways("A_MarkMap", 0, 1)
			"####" A 0 A_JumpIf(CallACS("A_CheckMapped") > 0, "MappingLoop")
			"####" A 0 A_CustomMissile("MapView", 0, 0, 135)
			"####" A 0 A_CustomMissile("MapView", 0, 0, -135)
			"####" A 0 A_CustomMissile("MapView", 0, 0, 112)
			"####" A 0 A_CustomMissile("MapView", 0, 0, -112)
			"####" A 0 A_CustomMissile("MapView", 0, 0, 45)
			"####" A 0 A_CustomMissile("MapView", 0, 0, -45)
			"####" A 0 A_CustomMissile("MapView", 0, 0, 90)
			"####" A 0 A_CustomMissile("MapView", 0, 0, -90)
		Bounce:
			Goto MappingLoop
		Dead:
			"####" A 0
			Stop
	}
}